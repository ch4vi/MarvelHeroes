plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

def STRING = "String"
def BOOLEAN = "boolean"
def appName = "Marvel Heroes"

android {
    compileSdk rootProject.ext.sdk.compile

    defaultConfig {
        applicationId "com.xavi.marvelheroes"
        minSdkVersion rootProject.ext.sdk.min
        targetSdkVersion rootProject.ext.sdk.target
        versionCode computeVersionCode()
        versionName rootProject.ext.version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        def publicKey = "\" \""
        def privateKey = "\" \""
        if (project.rootProject.file('local.properties').exists()) {
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            publicKey = properties.getProperty('marvel.pub')
            privateKey = properties.getProperty('marvel.pri')
        }

        buildConfigField STRING, "BASE_URL", "\"https://gateway.marvel.com/v1/public/\""
        buildConfigField STRING, "KEY_PUBLIC", publicKey
        buildConfigField STRING, "KEY_PRIVATE", privateKey
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            debuggable true

            resValue "string", "app_label", "[D] ${appName}"

            buildConfigField BOOLEAN, "NETWORK_LOGGING", "true"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "app_label", appName

            buildConfigField BOOLEAN, "NETWORK_LOGGING", "false"
        }
    }
    applicationVariants.all { variant ->
        outputs.all { output ->
            println("**************************************************")
            println("------------- ${variant.buildType.name} -------------")

            def outputName = "${appName}-${output.variantOutput.fullName}-${defaultConfig.versionName}.apk"
            if (variant.buildType.name == "debug") {
                outputName = "${appName}-${output.variantOutput.fullName}-${defaultConfig.versionCode}.apk"
            }
            output.outputFileName = outputName
            println(outputName)

            def versionInfo = "v${defaultConfig.versionName}"
            if (variant.buildType.name == "debug") {
                versionInfo = "v${defaultConfig.versionName}.${defaultConfig.versionCode} - ${output.variantOutput.fullName}"
            }
            variant.buildConfigField(STRING, "VERSION_INFO", "\"${versionInfo}\"")
            println(versionInfo)
            println("**************************************************")
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lint {
        lintConfig file('../config/lint.xml')
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation libs.bundles.common
    implementation libs.bundles.ui

    kapt libs.moshi.kapt
    kapt libs.room.kapt
    implementation libs.bundles.data

    testImplementation libs.bundles.test
}

def computeVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}