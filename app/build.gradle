plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

def STRING = "String"
def BOOLEAN = "boolean"
def appName = "Marvel Heroes"

android {
    compileSdk rootProject.ext.sdk.compile

    defaultConfig {
        applicationId "com.xavi.marvelheroes"
        minSdkVersion rootProject.ext.sdk.min
        targetSdkVersion rootProject.ext.sdk.target
        versionCode computeVersionCode()
        versionName rootProject.ext.version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        def publicKey = "\" \""
        def privateKey = "\" \""
        if (project.rootProject.file('local.properties').exists()) {
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            publicKey = properties.getProperty('marvel.pub')
            privateKey = properties.getProperty('marvel.pri')
        }

        buildConfigField STRING, "BASE_URL", "\"https://gateway.marvel.com/v1/public/\""
        buildConfigField STRING, "KEY_PUBLIC", publicKey
        buildConfigField STRING, "KEY_PRIVATE", privateKey
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            debuggable true

            resValue "string", "app_label", "[D] ${appName}"

            buildConfigField BOOLEAN, "NETWORK_LOGGING", "true"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "app_label", appName

            buildConfigField BOOLEAN, "NETWORK_LOGGING", "false"
        }
    }
    applicationVariants.all { variant ->
        outputs.all { output ->
            println("**************************************************")
            println("------------- ${variant.buildType.name} -------------")

            def outputName = "${appName}-${output.variantOutput.fullName}-${defaultConfig.versionName}.apk"
            if (variant.buildType.name == "debug") {
                outputName = "${appName}-${output.variantOutput.fullName}-${defaultConfig.versionCode}.apk"
            }
            output.outputFileName = outputName
            println(outputName)

            def versionInfo = "v${defaultConfig.versionName}"
            if (variant.buildType.name == "debug") {
                versionInfo = "v${defaultConfig.versionName}.${defaultConfig.versionCode} - ${output.variantOutput.fullName}"
            }
            variant.buildConfigField(STRING, "VERSION_INFO", "\"${versionInfo}\"")
            println(versionInfo)
            println("**************************************************")
        }
    }
    lintOptions {
        lintConfig file("../config/lint.xml")
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    // common
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "io.insert-koin:koin-android:3.1.6"
    implementation "com.jakewharton.timber:timber:4.7.1"

    // ui
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.2"
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"
    implementation "com.google.android.material:material:1.7.0-alpha02"
    implementation "io.coil-kt:coil:2.0.0-rc03"

    // data
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"
    implementation "com.squareup.moshi:moshi-adapters:1.13.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:4.9.3"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"

    // room
    kapt "androidx.room:room-compiler:2.4.2"
    implementation "androidx.room:room-ktx:2.4.2"
    implementation "androidx.room:room-runtime:2.4.2"
    implementation "androidx.room:room-paging:2.5.0-alpha01"

    // test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.insert-koin:koin-test-junit4:3.1.6"
    testImplementation "io.mockk:mockk:1.12.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

def computeVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}